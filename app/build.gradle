import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repos.zeroturnaround.com/nexus/content/repositories/zt-public-releases' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${ANDROID_GRADLE_PLUGIN_VERSION}"
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.5.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'android-apt'
apply plugin: 'com.vanniktech.android.junit.jacoco'

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.sulong.elecouple"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionName VERSION_NAME
        versionCode VERSION_CODE as int
        testApplicationId "com.sulong.elecouple.test"
        multiDexEnabled true
        buildConfigField "boolean", "PRINT_LOG", "true"
        manifestPlaceholders = [JPUSH_APPKEY_VALUE: "495ba12d57e820ba926b6e29"]
        ndk {
            abiFilters "armeabi-v7a", "x86", "armeabi"
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "PRINT_LOG", "true"
            buildConfigField "String", "SERVER_TYPE", '"testshop"'
            versionNameSuffix "1"
            signingConfig signingConfigs.release
            testCoverageEnabled true
        }
        dev.initWith(buildTypes.release)
        dev {
            buildConfigField "boolean", "PRINT_LOG", "true"
            buildConfigField "String", "SERVER_TYPE", '"o2odev"'
            versionNameSuffix "2" + "${BUILD_STAGE}"
        }
        release {
            buildConfigField "boolean", "PRINT_LOG", "false"
            buildConfigField "String", "SERVER_TYPE", '"release"'
            minifyEnabled true
            proguardFiles file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
        innerTest.initWith(buildTypes.release)
        innerTest {
            buildConfigField "boolean", "PRINT_LOG", "true"
            buildConfigField "String", "SERVER_TYPE", '"testshop"'
            versionNameSuffix "3" + "${BUILD_STAGE}"
        }
        release {
            buildConfigField "boolean", "PRINT_LOG", "false"
            buildConfigField "String", "SERVER_TYPE", '"release"'
            minifyEnabled true
            proguardFiles file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "2g"
    }

    /** Rename output file names */
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // 更改最终apk命名
            String appName = project.name
            String vCode = variant.versionCode
            String vName = variant.versionName
            String buildTypeName = variant.buildType.name
            String flavorName = null
            if (variant.productFlavors && !variant.productFlavors.isEmpty()) {  // productFlavors可能有多个维度，但现在实际用只会用1个维度，所以只取第一个flavorName
                flavorName = variant.productFlavors[0]?.name
            }
            String apkFileName = "${appName}_${vCode}_${vName}.apk"
            if (flavorName) { // 如果配置了productFlavors就加上flavorName
                apkFileName = "${appName}_${flavorName}_${vCode}_${vName}.apk"
            }
            if (!apkFileName.contains(buildTypeName)) { // 如果apk文件名中没有buildType名称就在末尾加上
                apkFileName = apkFileName.replace(".apk", "_${buildTypeName}.apk")
            }
            output.outputFile = new File(output.outputFile.parent, apkFileName)

            // 打完混淆包后来干一些事情
            if (variant.buildType.minifyEnabled) {
                variant.assemble.doLast {
                    // 定义文件拷贝方法
                    def copyFile = { File source, File dest ->
                        if (source.exists()) {
                            dest.withDataOutputStream { os ->
                                source.withDataInputStream { is ->
                                    os << is
                                }
                            }
                        }
                    }
                    // 混淆的mapping文件拷贝一份出来保存着
                    File sourceMappingFile = new File("${output.outputFile.parentFile.parent}/mapping/${variant.dirName}/mapping.txt")
                    File destMappingFile = new File(project.projectDir, "map.txt")
                    copyFile(sourceMappingFile, destMappingFile)

                    // 混淆的seeds文件拷贝一份出来保存着
                    File sourceSeedsFile = new File("${output.outputFile.parentFile.parent}/mapping/${variant.dirName}/seeds.txt")
                    File destSeedsFile = new File(project.projectDir, sourceSeedsFile.name)
                    copyFile(sourceSeedsFile, destSeedsFile)

                    // apk文件拷贝一份出来保存着
                    File destTestDir = new File(rootDir.absolutePath + "/build/apk4test/")
                    destTestDir.mkdirs()
                    File destTestApkFile = new File(destTestDir, getDate() + "_" + apkFileName)
                    copyFile(output.outputFile, destTestApkFile)
                }
            }
        }
    }
    productFlavors {
        yingyongbao {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "yingyongbao"]
        }
        market360 {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "market360"]
        }
        baidu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "baidu"]
        }
        xiaomi {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
        }
        huawei {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "huawei"]
        }
        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
        lianxiang {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "lianxiang"]
        }
        update {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "update"]
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'asm-license.txt'
    }

}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:25.0.1'
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:gridlayout-v7:25.0.1'
    compile 'com.android.support:cardview-v7:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:multidex:1.0.1'

    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.github.hotchemi:permissionsdispatcher:2.1.3'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.2'
    compile 'com.trello:rxlifecycle:1.0'
    compile 'com.trello:rxlifecycle-android:1.0'
    compile 'com.trello:rxlifecycle-components:1.0'
    compile 'com.liulishuo.filedownloader:library:0.3.1'
    compile 'com.jakewharton:butterknife:8.2.0'
    compile 'com.google.dagger:dagger:2.5'

    compile 'com.github.citux:datetimepicker:0.1.2'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.daimajia.swipelayout:library:1.2.0@aar'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'cn.lankton:flowlayout:2.0.0'
    compile 'com.wx.goodview:goodview:1.0.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    apt "com.github.hotchemi:permissionsdispatcher-processor:2.1.3"
    apt 'com.google.dagger:dagger-compiler:2.5'
    apt 'com.jakewharton:butterknife-compiler:8.2.0'

    // 添加内存泄露检测库
    android.buildTypes.each { buildType ->
        if (!buildType.name.equals("debug")) {
            owner.delegate."${buildType.name}Compile" 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
        } else {
            owner.delegate."${buildType.name}Compile" 'com.squareup.leakcanary:leakcanary-android:1.3.1'
        }
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile project(':AppUpdate')
    compile project(':logger')
    compile project(':qrcode')
    compile project(':wheel')
    compile project(':ptr-lib')
    compile project(':async-http')
    compile project(':LoadMoreLib')
    compile project(':PagerSlidingTabStripLib')
    compile project(':archartengine')
    compile project(':UmengSocialSDK')
    compile project(':richeditor')
    compile project(':CmbKeyboard')
}
